module PetriNetsOld;

create Migrated : After from Original : Before;

rule Nets {
	from
		o : Before!Net
	to
		m : After!Net (
			places <- o.places->collect(p | thisModule.Places(p)),
			transitions <- o.transitions->collect(t | thisModule.Transitions(t))
		)
}

unique lazy rule Places {
	from
		o : Before!Place
	to
		m : After!Place (
			net  <- o.net,
			name <- o.name
		)
}

unique lazy rule Transitions {
	from
		o : Before!Transition  
	to
		m : After!Transition (
			net  <- o.net,
			name <- o.name,
			"in" <- o.src->collect(p | thisModule.PTArcs(p,o)),
			out  <- o.dst->collect(p | thisModule.TPArcs(o,p))
		)
}

unique lazy rule PTArcs {
	from
		place       : Before!Place,
		destination : Before!Transition
	to
		ptarcs : After!PTArc (
			src <- thisModule.Places(place),
			dst <- destination,
			net <- destination.net
		)
}

unique lazy rule TPArcs {
	from
		transition  : Before!Transition,
		destination : Before!Place
	to
		tparcs : After!TPArc (
			src <- transition,
			dst <- thisModule.Places(destination),
			net <- transition.net
		)
}